@ECHO OFF
REM
REM Copyright (c) Contributors to the Open 3D Engine Project.
REM For complete copyright and license terms please see the LICENSE at the root of this distribution.
REM
REM SPDX-License-Identifier: Apache-2.0 OR MIT
REM
REM

SETLOCAL EnableDelayedExpansion

CALL %~dp0env_windows.cmd

IF NOT EXIST %OUTPUT_DIRECTORY% (
    ECHO [ci_build] Error: $OUTPUT_DIRECTORY was not found
    GOTO :error
)
PUSHD %OUTPUT_DIRECTORY%

REM Override the temporary directory used by wix to the workspace (if we have a WORKSPACE_TMP)
IF NOT "%WORKSPACE_TMP%"=="" (
    SET "WIX_TEMP=!WORKSPACE_TMP!/wix"
    IF NOT EXIST "%WIX_TEMP%" (
        MKDIR "%WIX_TEMP%"
    )
)

ECHO [ci_build] cpack --version
cpack --version
IF ERRORLEVEL 1 (
    ECHO [ci_build] CPack not found!
    GOTO :popd_error
)

ECHO [ci_build] cpack -C %CONFIGURATION% %CPACK_OPTIONS%
cpack  -C %CONFIGURATION% %CPACK_OPTIONS%
IF NOT %ERRORLEVEL%==0 (
    REM dump the log file generated by cpack specifically for WIX
    ECHO ****************************************************************
    TYPE "_CPack_Packages\\WIX\\wix.log"
    ECHO ****************************************************************
    GOTO :popd_error
)

POPD
EXIT /b 0

:popd_error
POPD

:error
EXIT /b 1
