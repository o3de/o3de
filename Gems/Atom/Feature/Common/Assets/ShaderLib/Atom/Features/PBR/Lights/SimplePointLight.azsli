/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

#include <Atom/Features/PBR/Lights/LightTypesCommon.azsli>

#ifndef SimplePointLightUtil
#define SimplePointLightUtil SimplePointLightUtil_PBR
#endif


class SimplePointLightUtil_PBR
{
    real3 surfaceToLightDirection;
    real  lightDistanceSquared;
    real  falloff;

    static SimplePointLightUtil_PBR Init(SimplePointLight light, Surface surface, float3 cameraPositionWS)
    {
        SimplePointLightUtil_PBR result;

        real3 posToLight = real3(light.m_position - surface.position);
        result.lightDistanceSquared = dot(posToLight, posToLight); // light distance squared
        result.falloff = result.lightDistanceSquared * real(light.m_invAttenuationRadiusSquared);
        result.surfaceToLightDirection = normalize(posToLight);
        return result;
    }

    real3 GetSurfaceToLightDirection()
    {
        return surfaceToLightDirection;
    }

    real GetFalloff()
    {
        return falloff;
    }

    void Apply(SimplePointLight light, Surface surface, real litRatio, inout LightingData lightingData)
    {
        // Smoothly adjusts the light intensity so it reaches 0 at light.m_attenuationRadius distance
        real radiusAttenuation = 1.0 - (falloff * falloff);
        radiusAttenuation = radiusAttenuation * radiusAttenuation;
        
        // Standard quadratic falloff
        lightDistanceSquared = max(0.001 * 0.001, lightDistanceSquared); // clamp the light to at least 1mm away to avoid extreme values.
        real3 lightIntensity = (real3(light.m_rgbIntensityCandelas) / lightDistanceSquared) * radiusAttenuation;

        // Diffuse contribution
        lightingData.diffuseLighting += GetDiffuseLighting(surface, lightingData, lightIntensity, surfaceToLightDirection) * litRatio;

        // Specular contribution
        lightingData.specularLighting += GetSpecularLighting(surface, lightingData, lightIntensity, surfaceToLightDirection) * litRatio;
    }
};

