/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

//! Transform cubemap coordinates to the expected cubemap orientation
float3 GetCubemapCoords(float3 original)
{
    return float3(-original.x, original.z, -original.y);
}

//! Compute multiscatter compensation multiplier
float3 GetMultiScatterCompensation(float3 specularF0, float2 brdf, bool enabled)
{
    if (!enabled)
    {
        // skip calculation if compensation is disabled
        return 1.0;
    }

    // returned values of BRDF are formed by split sum approximation as shown below
    // brdf.x = integral{(BRDF / F) * (1 - (1 - VdotH)^5) * NdotL dL} 
    // brdf.y = integral{(BRDF / F) * (1 - VdotH)^5 * NdotL dL}
    // brdf.x + brdf.y = integral{ (BRDF / F) * (1 - alpha + alpha) * NdotL dL }
    //                 = integral{ (BRDF / F) * NdotL dL }
    //                 = integral{ ((GD / 4 * NdotL * NdotV)) * NdotL dL }
    // which is the integral of microfacet BRDF by assuming fresnel term F == 1 that represents total single scattering reflectance
    // for more information about compensation term please see:
    // https://blog.selfshadow.com/publications/turquin/ms_comp_final.pdf
    float3 compensation = specularF0 * ((1.0 / (brdf.x + brdf.y)) - 1.0);
    return compensation + 1.0;
}

// returns the UV coordinates for the BRDF texture
float2 GetBRDFTexCoords(float roughness, float NdotV)
{
    float smoothness = 1.0f - roughness;
    return float2(saturate(NdotV), smoothness);
}

// returns the roughess mip for sampling the convolved specular cubemap
float GetRoughnessMip(float roughness)
{
    const float maxRoughnessMip = 5;
    return roughness * maxRoughnessMip;
}

// compute parallax corrected reflection vector, AABB version
// we do this by finding the intersection with the volume and adjusting the reflection vector for the surface position 
// https://seblagarde.wordpress.com/2012/09/29/image-based-lighting-approaches-and-parallax-corrected-cubemap/
float3 ApplyParallaxCorrectionAABB(float3 aabbMin, float3 aabbMax, float3 aabbPos, float3 positionWS, float3 reflectDir)
{
    float3 rcpReflectDir = 1.0f / reflectDir;
    float3 intersectA = (aabbMax - positionWS) * rcpReflectDir;
    float3 intersectB = (aabbMin - positionWS) * rcpReflectDir;
    float3 furthestIntersect = max(intersectA, intersectB);
    float distance = min(min(furthestIntersect.x, furthestIntersect.y), furthestIntersect.z);
    float3 intersectPos = reflectDir * distance + positionWS;
    return (intersectPos - aabbPos);
}

// compute parallax corrected reflection vector, OBB version
float3 ApplyParallaxCorrectionOBB(float4x4 obbTransformInverse, float3 obbHalfExtents, float3 positionWS, float3 reflectDir)
{
    float3 p = mul(obbTransformInverse, float4(positionWS, 1.0f)).xyz;
    return ApplyParallaxCorrectionAABB(-obbHalfExtents, obbHalfExtents, float3(0.0f, 0.0f, 0.0f), p, reflectDir);
}
