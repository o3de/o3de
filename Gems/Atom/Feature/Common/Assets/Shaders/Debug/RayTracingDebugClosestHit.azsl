/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#include <Atom/Features/RayTracing/RayTracingSrgs.azsli>
#include <Atom/Features/RayTracing/RayTracingSceneUtils.azsli>
#include <RayTracingDebugCommon.azsli>

VertexData GetVertexData(float2 barycentrics)
{
    RayTracingSceneSrg::MeshInfo meshInfo = RayTracingSceneSrg::m_meshInfo[NonUniformResourceIndex(InstanceIndex())];
    return GetHitInterpolatedVertexData(meshInfo, barycentrics);
}

float3 GetBarycentricColor(float2 barycentrics)
{
    return float3(barycentrics, 1.f - barycentrics.x - barycentrics.y);
}

float3 GetNormalColor(float2 barycentrics)
{
    VertexData vertexData = GetVertexData(barycentrics);
    return vertexData.m_normal * 0.5f + 0.5f;
}

float3 GetUVColor(float2 barycentrics)
{
    VertexData vertexData = GetVertexData(barycentrics);
    return float3(vertexData.m_uv, 0.f);
}

[shader("closesthit")]
void ClosestHit(inout PayloadData payload, BuiltInTriangleIntersectionAttributes attrib)
{
    switch (GetRayTracingDebugViewMode())
    {
    case RayTracingDebugViewMode::InstanceID:     payload.m_color = GetRandomColor(InstanceID());             break;
    case RayTracingDebugViewMode::InstanceIndex:  payload.m_color = GetRandomColor(InstanceIndex());          break;
    case RayTracingDebugViewMode::PrimitiveIndex: payload.m_color = GetRandomColor(PrimitiveIndex());         break;
    case RayTracingDebugViewMode::Barycentrics:   payload.m_color = GetBarycentricColor(attrib.barycentrics); break;
    case RayTracingDebugViewMode::Normals:        payload.m_color = GetNormalColor(attrib.barycentrics);      break;
    case RayTracingDebugViewMode::UVs:            payload.m_color = GetUVColor(attrib.barycentrics);          break;
    default:                                      payload.m_noData = true;                                    break;
    }
}
