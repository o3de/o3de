/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

// refer https://github.com/GPUOpen-Effects/FidelityFX-SPD/blob/c52944f547884774a1b33066f740e6bf89f927f5/sample/src/DX12/SPDIntegration.hlsl

#include <Atom/RPI/Math.azsli>
#include <Atom/Features/SrgSemantics.azsli>

struct SpdGlobalAtomicBuffer
{
    uint m_counter;
};

ShaderResourceGroup PassSrg : SRG_PerPass
{
    RWTexture2D<float4> m_imageDestination[13];
    [[verbatim("globallycoherent")]] RWTexture2D<float4> m_mip6;
    [[verbatim("globallycoherent")]] RWStructuredBuffer<SpdGlobalAtomicBuffer> m_globalAtomic;

    uint m_mips;
    uint m_numWorkGroups;
    uint2 m_workGroupOffset;

    uint2 m_imageSize;
}

#define A_GPU
#define A_HLSL
#include <3rdParty/Features/PostProcessing/ffx-spd/ffx_a.h>

groupshared AU1 s_spdCounter;

groupshared AF1 s_spdIntermediateR[16][16];
groupshared AF1 s_spdIntermediateG[16][16];
groupshared AF1 s_spdIntermediateB[16][16];
groupshared AF1 s_spdIntermediateA[16][16];

AF4 SpdLoadSourceImage(AF2 tex, AU1 /* slice */)
{
    if (tex.x < PassSrg::m_imageSize.x &&
        tex.y < PassSrg::m_imageSize.y)
    {
        float4 value = PassSrg::m_imageDestination[0][tex];
        value.w = 1.;
        return value;
    }
    else
    {
        return AF4(FLOAT_32_MAX, 0., 0., 0.);
    }
}

AF4 SpdLoad(ASU2 tex, AU1 /* slice */)
{
    return PassSrg::m_mip6[tex];
}

void SpdStore(ASU2 pix, AF4 value, AU1 mip, AU1 /* slice */)
{
    // Store to the globally coherent working mip slice.
    if (mip == 5 && value.w > 0.)
        PassSrg::m_mip6[pix] = value;
    
    const AU1 mipAdjusted = min(mip + 1, PassSrg::m_mips - 1);
    PassSrg::m_imageDestination[mipAdjusted][pix] = value;
}

void SpdIncreaseAtomicCounter(AU1 /* slice*/)
{
    InterlockedAdd(PassSrg::m_globalAtomic[0].m_counter, 1, s_spdCounter);
}

AU1 SpdGetAtomicCounter()
{
    return s_spdCounter;
}

void SpdResetAtomicCounter(AU1 /* slice */)
{
    PassSrg::m_globalAtomic[0].m_counter = 0;
}

AF4 SpdLoadIntermediate(AU1 x, AU1 y)
{
    return AF4(
        s_spdIntermediateR[x][y],
        s_spdIntermediateG[x][y],
        s_spdIntermediateB[x][y],
        s_spdIntermediateA[x][y]);
}

void SpdStoreIntermediate(AU1 x, AU1 y, AF4 value)
{
    s_spdIntermediateR[x][y] = value.x;
    s_spdIntermediateG[x][y] = value.y;
    s_spdIntermediateB[x][y] = value.z;
    s_spdIntermediateA[x][y] = value.w;
}

float GetValueWithFlag(float value, float flag, float errorValue)
{
    return (flag > 0.) ? value : errorValue;
}

float MinWithFlag(AF4 v0, AF4 v1)
{
    return min(
        GetValueWithFlag(v0.x, v0.w, FLOAT_32_MAX),
        GetValueWithFlag(v1.x, v1.w, FLOAT_32_MAX));
}

float MaxWithFlag(AF4 v0, AF4 v1)
{
    return max(
        GetValueWithFlag(v0.z, v0.w, 0.),
        GetValueWithFlag(v1.z, v1.w, 0.));
}

AF4 SpdReduce4(AF4 v0, AF4 v1, AF4 v2, AF4 v3)
{
    const float weightSum = v0.w + v1.w + v2.w + v3.w;
    const float minValue = min(MinWithFlag(v0, v1), MinWithFlag(v2, v3));
    const float avgValue = weightSum > 0 ? 
        (v0.y * v0.w + v1.y * v1.w + v2.y * v2.w + v3.y * v3.w) / weightSum :
        0.f;
    const float maxValue = max(MaxWithFlag(v0, v1), MaxWithFlag(v2, v3));
    const float weightValue =  weightSum * 0.25;
    return AF4(minValue, avgValue, maxValue, weightValue);
}

#include <3rdParty/Features/PostProcessing/ffx-spd/ffx_spd.h>
 
[numthreads(256, 1, 1)]
void MainCS(uint3 workGroupId : SV_GroupID, uint localThreadIndex : SV_GroupIndex)
{
    if (workGroupId.x * 64 >= PassSrg::m_imageSize.x ||
        workGroupId.y * 64 >= PassSrg::m_imageSize.y)
    {
        if (localThreadIndex == 0)
        {
            SpdIncreaseAtomicCounter(0);
        }    
        return;
    } 
    SpdDownsample(
        AU2(workGroupId.xy), 
        AU1(localThreadIndex),  
        AU1(PassSrg::m_mips),
        AU1(PassSrg::m_numWorkGroups),
        AU1(workGroupId.z),
        AU2(PassSrg::m_workGroupOffset));
}
