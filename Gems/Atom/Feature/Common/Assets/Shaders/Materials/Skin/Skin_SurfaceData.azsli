/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

// This #define magic lets you use the Surface class in this file without making it the final Surface
// used in your shader. Simply #define Surface to your custom definition before including this file
//
#ifndef Surface
#define Surface     SurfaceData_Skin
#endif

#include "../BasePBR/BasePBR_SurfaceData.azsli"
#include <Atom/Features/PBR/Surfaces/TransmissionSurfaceData.azsli>

class SurfaceData_Skin
{
    TransmissionSurfaceData transmission;

    // ------- BasePbrSurfaceData -------
    
    float3 position;            //!< Position in world-space
    float3 normal;              //!< Normal in world-space
    float3 vertexNormal;        //!< Vertex normal in world-space
    float3 albedo;              //!< Albedo color of the non-metallic material, will be multiplied against the diffuse lighting value
    float3 specularF0;          //!< Fresnel f0 spectral value of the surface
    float roughnessLinear;      //!< Perceptually linear roughness value authored by artists. Must be remapped to roughnessA before use
    float roughnessA;           //!< Actual roughness value ( a.k.a. "alpha roughness") to be used in microfacet calculations
    float roughnessA2;          //!< Alpha roughness ^ 2 (i.e. roughnessA * roughnessA), used in GGX, cached here for perfromance

    float3 baseColor;
    float metallic;

    //! Subsurface scattering parameters
    float subsurfaceScatteringFactor;
    float subsurfaceScatteringQuality;
    float3 scatterDistance;

    //! Calculates roughnessA and roughnessA2 after roughness has been set
    void CalculateRoughnessA();

    //! Sets albedo and specularF0 (no metallic for skin)
    void SetAlbedoAndSpecularF0(float3 newBaseColor, float specularF0Factor);

    //! Get the packed subsurface scattering factor and quality
    float GetSubsurfaceScatteringFactorAndQuality();
};

void SurfaceData_Skin::CalculateRoughnessA()
{
    CalculateRoughnessValues(normal, roughnessLinear, roughnessA, roughnessA2);
}

void SurfaceData_Skin::SetAlbedoAndSpecularF0(float3 newBaseColor, float specularF0Factor)
{
    baseColor = newBaseColor;
    albedo = newBaseColor;
    specularF0 = MaxDielectricSpecularF0 * specularF0Factor;
    metallic = 0;
}

float SurfaceData_Skin::GetSubsurfaceScatteringFactorAndQuality()
{
    // Pack factor and quality, drawback: because of precision limit of float16 cannot represent exact 1, maximum representable value is 0.9961
    uint factorAndQuality = dot(round(float2(saturate(subsurfaceScatteringFactor), subsurfaceScatteringQuality) * 255), float2(256, 1));
    return factorAndQuality * (o_enableSubsurfaceScattering ? 1.0 : -1.0);
}
