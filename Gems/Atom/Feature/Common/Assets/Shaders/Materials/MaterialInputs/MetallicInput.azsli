/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

#include <Atom/Features/Debug.azsli>

// This file provides utilities for common handling of inputs for "metalness" of PBR materials.

// These macros can be used to declare common shader inputs for this feature. 
// Use the COMMON_SRG_INPUTS_* macro in your material SRG definition, and use the COMMON_OPTIONS_* macro at the global scope in your shader. Then you can pass these variables to the Get*Input() function below.
// You can optionally provide a prefix for the set of inputs which corresponds to a prefix string supplied by the .materialtype file. This is common for multi-layered material types.

#define COMMON_SRG_INPUTS_METALLIC(prefix) \
float       prefix##m_metallicFactor;      \
Texture2D   prefix##m_metallicMap;         \
uint        prefix##m_metallicMapUvIndex;

#define COMMON_OPTIONS_METALLIC(prefix) \
option bool prefix##o_metallic_useTexture; 

real GetMetallicInput(Texture2D map, sampler mapSampler, float2 uv, real factor, bool useTexture, float4 uvDxDy = float4(0.0, 0.0, 0.0, 0.0), bool customDerivatives = false)
{
    if(OverrideMetallicEnabled())
    {
        return GetMetallicOverride();
    }

    if (useTexture)
    {
       // [GFX TODO][ATOM-1793]: Figure out how we want our base material to expect channels to be encoded, and apply that to the way we pack metalness.
       if (customDerivatives)
       {
           return real(map.SampleGrad(mapSampler, uv, uvDxDy.xy, uvDxDy.zw).r);
       }
       else
       {
           return real(map.Sample(mapSampler, uv).r);
       }
    }
    return factor;
}
