/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

#include <Atom/Features/ParallaxMapping.azsli>
#include "../MaterialInputs/ParallaxInput.azsli"
#include <Atom/Features/MatrixUtility.azsli>
#include "ParallaxShadowUtil.azsli"

 void SetPixelDepth(
     const MaterialParameters params,
     inout float3 positionWS,
     real3 normal,
     float3 tangents[UvSetCount],
     float3 bitangents[UvSetCount],
     inout float2 uvs[UvSetCount],
     bool isFrontFace,
     float4x4 objectToWorld,
     inout float depthNDC,
     inout float depthCS,
     out bool isClipped)
{
    real3x3 uvMatrix = params.m_parallaxUvIndex == 0 ? real3x3(params.m_uvMatrix) : CreateIdentity3x3_real();
    real3x3 uvMatrixInverse = params.m_parallaxUvIndex == 0 ? real3x3(params.m_uvMatrixInverse) : CreateIdentity3x3_real();
    
    float heightmapScale = float(params.m_heightmapScale);
    float heightmapOffset = float(params.m_heightmapOffset);
    
    AdjustParallaxShadowCaster(heightmapScale, heightmapOffset, float(params.m_parallax_pdo_shadowFactor));

    GetParallaxInput(
        params,
        GetMaterialTexture(params.m_heightmap), GetMaterialTextureSampler(), normal, tangents[params.m_parallaxUvIndex], bitangents[params.m_parallaxUvIndex],
        heightmapScale, heightmapOffset,
        objectToWorld, uvMatrix, uvMatrixInverse,
        uvs[params.m_parallaxUvIndex], positionWS, depthNDC, depthCS, isClipped);
}

 void SetPixelDepth(
     const MaterialParameters params,
     inout float3 positionWS,
     real3 normal,
     float3 tangents[UvSetCount],
     float3 bitangents[UvSetCount],
     inout float2 uvs[UvSetCount],
     bool isFrontFace,
     float4x4 objectToWorld,
     inout float depthNDC)
{
    // Dummy variables to call the above function
    float depthCS = 1.0;
    bool isClipped = false;
    
    SetPixelDepth(params, positionWS, normal, tangents, bitangents, uvs, isFrontFace, objectToWorld, depthNDC, depthCS, isClipped);
}

 void SetPixelDepth(
     const MaterialParameters params,
     inout float3 positionWS,
     float3 normal,
     float3 tangents[UvSetCount],
     float3 bitangents[UvSetCount],
     inout float2 uvs[UvSetCount],
     inout float2 detailUv[UvSetCount],
     bool isFrontFace,
     float4x4 objectToWorld,
     inout float depthNDC,
     inout float depthCS,
     out bool isClipped)
{
    // GetParallaxInput applies an tangent offset to the UV. We want to apply the same offset to the detailUv (note: this needs to be tested with content)
    // The math is:   offset = newUv - oldUv;   detailUv += offset;
    // This is the same as:   detailUv -= oldUv;   detailUv += newUv;
    detailUv[params.m_parallaxUvIndex] -= uvs[params.m_parallaxUvIndex];
    
    SetPixelDepth(params, positionWS, normal, tangents, bitangents, uvs, isFrontFace, objectToWorld, depthNDC, depthCS, isClipped);

    // Apply second part of the offset to the detail UV (see comment above)
    detailUv[params.m_parallaxUvIndex] += uvs[params.m_parallaxUvIndex];
}
