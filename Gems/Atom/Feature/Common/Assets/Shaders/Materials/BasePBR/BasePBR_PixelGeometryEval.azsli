/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

// This #define magic lets you use the EvaluatePixelGeometry function in this file without making it the final EvaluatePixelGeometry
// used in your shader. Simply #define EvaluatePixelGeometry to your custom definition before including this file
//
#ifndef EvaluatePixelGeometry
#define EvaluatePixelGeometry(IN, isFrontFace)      EvaluatePixelGeometry_BasePBR(IN, isFrontFace)
#endif

#include "../MaterialFunctions/EvaluateTangentFrame.azsli"

PixelGeometryData EvaluatePixelGeometry_BasePBR(
    float3 positionWS,
    float3 normal,
    float3 tangent,
    float3 bitangent,
    float2 uvs[UvSetCount],
    bool isFrontFace,
    bool evaluateTangentFrame)
{
    PixelGeometryData geoData;

    geoData.positionWS = positionWS;
    geoData.vertexNormal = normalize(normal);

    geoData.tangents[0] = tangent;
    geoData.tangents[1] = tangent;

    geoData.bitangents[0] = bitangent;
    geoData.bitangents[1] = bitangent;

    geoData.uvs = uvs;
    geoData.isFrontFace = isFrontFace;

    if (evaluateTangentFrame)
    {
        for (int i = 0; i != UvSetCount; ++i)
        {
            EvaluateTangentFrame(geoData.vertexNormal, positionWS, isFrontFace, uvs[i], i,
                tangent, bitangent, geoData.tangents[i], geoData.bitangents[i]);
        }
    }

    return geoData;
}

PixelGeometryData EvaluatePixelGeometry_BasePBR(
    float3 positionWS,
    float3 normal,
    float3 tangent,
    float3 bitangent,
    float2 uvs[UvSetCount],
    bool isFrontFace)
{
    bool evaluateTangentFrame = o_normal_useTexture;
    return EvaluatePixelGeometry_BasePBR(positionWS, normal, tangent, bitangent, uvs, isFrontFace, evaluateTangentFrame);
}

PixelGeometryData EvaluatePixelGeometry_BasePBR(VsOutput IN, bool isFrontFace)
{
    float4x4 objectToWorld = ObjectSrg::GetWorldMatrix();
    float3x3 objectToWorldIT = ObjectSrg::GetWorldMatrixInverseTranspose();

    float3 vertexNormal, vertexTangent, vertexBitangent;
    ConstructTBN(IN.normal, IN.tangent, objectToWorld, objectToWorldIT, vertexNormal, vertexTangent, vertexBitangent);

    return EvaluatePixelGeometry_BasePBR(
        IN.worldPosition,
        vertexNormal,
        vertexTangent,
        vertexBitangent,
        IN.uvs,
        isFrontFace);
}
