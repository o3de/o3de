/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

// This #define magic lets you use the EvaluateSurface function in this file without making it the final EvaluateSurface
// used in your shader. Simply #define EvaluateSurface to your custom definition before including this file
//
#ifndef EvaluateSurface
#define EvaluateSurface EvaluateSurface_EnhancedPBR
#endif

#include "../StandardPBR/StandardPBR_SurfaceEval.azsli"
#include "../MaterialFunctions/StandardGetAlphaAndClip.azsli"

Surface EvaluateSurface_EnhancedPBR(
    float3 positionWS,
    real3 normalWS,
    float3 tangents[UvSetCount],
    float3 bitangents[UvSetCount],
    float2 uvs[UvSetCount],
    float2 detailUvs[UvSetCount],
    bool isFrontFace,
    bool isDisplacementClipped,
    float4 uvDxDy,
    bool customDerivatives,
    float4 vertexColor)
{
    Surface surface;
    surface.position = positionWS;

    // ------- Detail Layer Setup -------
    
    const float2 detailUv = detailUvs[MaterialSrg::m_detail_allMapsUvIndex];

    // When the detail maps and the detail blend mask are on the same UV, they both use the transformed detail UVs because they are 'attached' to each other
    const float2 detailBlendMaskUv = (MaterialSrg::m_detail_blendMask_uvIndex == MaterialSrg::m_detail_allMapsUvIndex) ?
        detailUvs[MaterialSrg::m_detail_blendMask_uvIndex] : 
        uvs[MaterialSrg::m_detail_blendMask_uvIndex];        

    const real detailLayerBlendFactor = GetDetailLayerBlendFactor(
        MaterialSrg::m_detail_blendMask_texture,
        MaterialSrg::m_sampler,
        detailBlendMaskUv,
        o_detail_blendMask_useTexture,
        real(MaterialSrg::m_detail_blendFactor),
        uvDxDy,
        customDerivatives);

    // ------- Normal -------
    
    surface.vertexNormal = real3(normalWS);
    float2 normalUv = uvs[MaterialSrg::m_normalMapUvIndex];
    real3x3 uvMatrix = MaterialSrg::m_normalMapUvIndex == 0 ? real3x3(MaterialSrg::m_uvMatrix) : CreateIdentity3x3_real(); // By design, only UV0 is allowed to apply transforms.
    real detailLayerNormalFactor = real(MaterialSrg::m_detail_normal_factor) * detailLayerBlendFactor;
    float3 normal = GetDetailedNormalInputWS(
        isFrontFace, normalWS,
        tangents[MaterialSrg::m_normalMapUvIndex],      bitangents[MaterialSrg::m_normalMapUvIndex],      MaterialSrg::m_normalMap,             MaterialSrg::m_sampler, normalUv, MaterialSrg::m_normalFactor,  MaterialSrg::m_flipNormalX,         MaterialSrg::m_flipNormalY,         uvMatrix,                                o_normal_useTexture,
        tangents[MaterialSrg::m_detail_allMapsUvIndex], bitangents[MaterialSrg::m_detail_allMapsUvIndex], MaterialSrg::m_detail_normal_texture, MaterialSrg::m_sampler, detailUv, detailLayerNormalFactor,      MaterialSrg::m_detail_normal_flipX, MaterialSrg::m_detail_normal_flipY, real3x3(MaterialSrg::m_detailUvMatrix), o_detail_normal_useTexture,
        uvDxDy, customDerivatives);

    surface.normal = real3(normal);

    // ------- Base Color -------

    real detailLayerBaseColorFactor = real(MaterialSrg::m_detail_baseColor_factor) * detailLayerBlendFactor;
    float2 baseColorUv = uvs[MaterialSrg::m_baseColorMapUvIndex];

    real3 baseColor = GetDetailedBaseColorInput(
        MaterialSrg::m_baseColorMap,             MaterialSrg::m_sampler, baseColorUv, o_baseColor_useTexture,        real3(MaterialSrg::m_baseColor),  real(MaterialSrg::m_baseColorFactor), o_baseColorTextureBlendMode,
        MaterialSrg::m_detail_baseColor_texture, MaterialSrg::m_sampler, detailUv,    o_detail_baseColor_useTexture, detailLayerBaseColorFactor, uvDxDy, customDerivatives);
    baseColor = BlendVertexColor(baseColor, real3(vertexColor.rgb));
    
    // ------- Parallax Clipping -------

    if(o_parallax_highlightClipping && isDisplacementClipped)
    {
        ApplyParallaxClippingHighlight(baseColor);
    }

    // ------- Alpha & Clip -------

    // TODO: this often invokes a separate sample of the base color texture which is wasteful
    surface.alpha = GetAlphaAndClip(uvs, uvDxDy, customDerivatives);

    // ------- Metallic -------

    real metallic = 0;
    if(!o_enableSubsurfaceScattering)   // If subsurface scattering is enabled skip texture lookup for metallic, as this quantity won't be used anyway
    {
        float2 metallicUv = uvs[MaterialSrg::m_metallicMapUvIndex];
        metallic = GetMetallicInput(MaterialSrg::m_metallicMap, MaterialSrg::m_sampler, metallicUv, real(MaterialSrg::m_metallicFactor), o_metallic_useTexture, uvDxDy, customDerivatives);
    }

    // ------- Specular -------

    float2 specularUv = uvs[MaterialSrg::m_specularF0MapUvIndex];
    real specularF0Factor = GetSpecularInput(MaterialSrg::m_specularF0Map, MaterialSrg::m_sampler, specularUv, real(MaterialSrg::m_specularF0Factor), o_specularF0_useTexture, uvDxDy, customDerivatives);

    surface.SetAlbedoAndSpecularF0(baseColor, specularF0Factor, metallic);

    // ------- Roughness -------

    float2 roughnessUv = uvs[MaterialSrg::m_roughnessMapUvIndex];
    surface.roughnessLinear = GetRoughnessInput(MaterialSrg::m_roughnessMap, MaterialSrg::m_sampler, roughnessUv, real(MaterialSrg::m_roughnessFactor),
                                        real(MaterialSrg::m_roughnessLowerBound), real(MaterialSrg::m_roughnessUpperBound), o_roughness_useTexture, uvDxDy, customDerivatives);
    surface.CalculateRoughnessA();

    // ------- Subsurface -------

    float2 subsurfaceUv = uvs[MaterialSrg::m_subsurfaceScatteringInfluenceMapUvIndex];
    surface.subsurfaceScatteringFactor = GetSubsurfaceInput(MaterialSrg::m_subsurfaceScatteringInfluenceMap, MaterialSrg::m_sampler, subsurfaceUv, real(MaterialSrg::m_subsurfaceScatteringFactor), uvDxDy, customDerivatives);
    surface.subsurfaceScatteringQuality = real(MaterialSrg::m_subsurfaceScatteringQuality);
    surface.scatterDistance = real3(MaterialSrg::m_scatterDistance);

    // ------- Transmission -------
    
#if ENABLE_TRANSMISSION
    float2 transmissionUv = uvs[MaterialSrg::m_transmissionThicknessMapUvIndex];
    real4 transmissionTintThickness = GeTransmissionInput(MaterialSrg::m_transmissionThicknessMap, MaterialSrg::m_sampler, transmissionUv, real4(MaterialSrg::m_transmissionTintThickness), uvDxDy, customDerivatives);
    surface.transmission.tint = transmissionTintThickness.rgb;
    surface.transmission.thickness = transmissionTintThickness.w;
    surface.transmission.transmissionParams = real4(MaterialSrg::m_transmissionParams);
    surface.transmission.scatterDistance = real3(MaterialSrg::m_scatterDistance);
#endif

    // ------- Anisotropy -------

    if (o_enableAnisotropy)
    {
        // Convert the angle from [0..1] = [0 .. 180 degrees] to radians [0 .. PI]
        const real anisotropyAngle = real(MaterialSrg::m_anisotropicAngle) * PI;
        const real anisotropyFactor = real(MaterialSrg::m_anisotropicFactor);
        surface.anisotropy.Init(surface.GetSpecularNormal(), tangents[0], bitangents[0], anisotropyAngle, anisotropyFactor, surface.roughnessA);
    }

    // ------- Emissive -------

    float2 emissiveUv = uvs[MaterialSrg::m_emissiveMapUvIndex];
    surface.emissiveLighting = GetEmissiveInput(MaterialSrg::m_emissiveMap, MaterialSrg::m_sampler, emissiveUv, real(MaterialSrg::m_emissiveIntensity), real3(MaterialSrg::m_emissiveColor.rgb), real(MaterialSrg::m_emissiveAffectedByAlpha), surface.alpha,  o_emissiveEnabled, o_emissive_useTexture, uvDxDy, customDerivatives);

    // ------- Occlusion -------
    
    surface.diffuseAmbientOcclusion = GetOcclusionInput(MaterialSrg::m_diffuseOcclusionMap, MaterialSrg::m_sampler, uvs[MaterialSrg::m_diffuseOcclusionMapUvIndex], real(MaterialSrg::m_diffuseOcclusionFactor), o_diffuseOcclusion_useTexture, uvDxDy, customDerivatives);
    surface.specularOcclusion = GetOcclusionInput(MaterialSrg::m_specularOcclusionMap, MaterialSrg::m_sampler, uvs[MaterialSrg::m_specularOcclusionMapUvIndex], real(MaterialSrg::m_specularOcclusionFactor), o_specularOcclusion_useTexture, uvDxDy, customDerivatives);

    // ------- Clearcoat -------
    
#if ENABLE_CLEAR_COAT
    // Todo: Clean up the double uses of these clear coat flags
    if(o_clearCoat_feature_enabled)
    {
        if(o_clearCoat_enabled)
        {
            real3x3 uvMatrix = MaterialSrg::m_clearCoatNormalMapUvIndex == 0 ? real3x3(MaterialSrg::m_uvMatrix) : CreateIdentity3x3_real();
            GetClearCoatInputs(MaterialSrg::m_clearCoatInfluenceMap, uvs[MaterialSrg::m_clearCoatInfluenceMapUvIndex], real(MaterialSrg::m_clearCoatFactor), o_clearCoat_factor_useTexture,
                               MaterialSrg::m_clearCoatRoughnessMap, uvs[MaterialSrg::m_clearCoatRoughnessMapUvIndex], real(MaterialSrg::m_clearCoatRoughness), o_clearCoat_roughness_useTexture,
                               MaterialSrg::m_clearCoatNormalMap,    uvs[MaterialSrg::m_clearCoatNormalMapUvIndex], normalWS, o_clearCoat_normal_useTexture, real(MaterialSrg::m_clearCoatNormalStrength),
                               uvMatrix, tangents[MaterialSrg::m_clearCoatNormalMapUvIndex], bitangents[MaterialSrg::m_clearCoatNormalMapUvIndex],
                               MaterialSrg::m_sampler, isFrontFace,
                               surface.clearCoat.factor, surface.clearCoat.roughness, surface.clearCoat.normal, uvDxDy, customDerivatives);
        }
        
        ApplyClearCoatToSpecularF0(surface.specularF0, surface.clearCoat.factor);
    }
#endif
    
    // ------- Opacity -------

    surface.opacityAffectsSpecularFactor = real(MaterialSrg::m_opacityAffectsSpecularFactor);

    return surface;
}

// Helper functions to keep compatible with the previous version
// because dxc compiler doesn't allow default parameters on functions with overloads
Surface EvaluateSurface_EnhancedPBR(
    float3 positionWS,
    real3 normalWS,
    float3 tangents[UvSetCount],
    float3 bitangents[UvSetCount],
    float2 uvs[UvSetCount],
    float2 detailUvs[UvSetCount],
    bool isFrontFace,
    bool isDisplacementClipped,
    float4 uvDxDy,
    bool customDerivatives)
{
    return EvaluateSurface_EnhancedPBR(
            positionWS,
            normalWS,
            tangents,
            bitangents,
            uvs,
            detailUvs,
            isFrontFace,
            isDisplacementClipped,
            uvDxDy,
            customDerivatives,
            float4(1.0f, 1.0f, 1.0f, 1.0f));
}

Surface EvaluateSurface_EnhancedPBR(
    float3 positionWS,
    real3 normalWS,
    float3 tangents[UvSetCount],
    float3 bitangents[UvSetCount],
    float2 uvs[UvSetCount],
    float2 detailUvs[UvSetCount],
    bool isFrontFace,
    bool isDisplacementClipped)
{
    return EvaluateSurface_EnhancedPBR(
        positionWS,
        normalWS,
        tangents,
        bitangents,
        uvs,
        detailUvs,
        isFrontFace,
        isDisplacementClipped,
        float4(0.0f, 0.0f, 0.0f, 0.0f),
        false);
}

Surface EvaluateSurface_EnhancedPBR(VsOutput IN, PixelGeometryData geoData, float4 uvDxDy, bool customDerivatives)
{
    return EvaluateSurface_EnhancedPBR(
        geoData.positionWS,
        geoData.vertexNormal,
        geoData.tangents,
        geoData.bitangents,
        geoData.uvs,
        geoData.detailUvs,
        geoData.isFrontFace,
        geoData.isDisplacementClipped,
        uvDxDy,
        customDerivatives,
        GetVertexColor(IN));
}

Surface EvaluateSurface_EnhancedPBR(VsOutput IN, PixelGeometryData geoData)
{
    return EvaluateSurface_EnhancedPBR(
        geoData.positionWS,
        geoData.vertexNormal,
        geoData.tangents,
        geoData.bitangents,
        geoData.uvs,
        geoData.detailUvs,
        geoData.isFrontFace,
        geoData.isDisplacementClipped,
        float4(0.0f, 0.0f, 0.0f, 0.0f),
        false,
        GetVertexColor(IN));
}
