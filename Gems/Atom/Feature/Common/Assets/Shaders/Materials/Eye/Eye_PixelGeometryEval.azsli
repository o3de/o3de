/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

// This #define magic lets you use the EvaluatePixelGeometry function in this file without making it the final EvaluatePixelGeometry
// used in your shader. Simply #define EvaluatePixelGeometry to your custom definition before including this file
//
#ifndef EvaluatePixelGeometry
#define EvaluatePixelGeometry EvaluatePixelGeometry_Eye
#endif

#include "../MaterialFunctions/EvaluateTangentFrame.azsli"
#include <Atom/Features/InstancedTransforms.azsli>

// TODO: The body of this function is copy pasted from BasePBR_PixelGeometryEval.azsli as that file could not be included here
// since the file references an option o_normal_useTexture, which is not defined for the Eye shader. Need to find an elegant
// way to handle this.
PixelGeometryData EvaluatePixelGeometry_Eye(
    float3 positionWS,
    real3 normalWS,
    real3 tangentWS,
    real3 bitangentWS,
    float2 uvs[UvSetCount],
    bool isFrontFace,
    bool evaluateTangentFrame)
{
    PixelGeometryData geoData;

    geoData.positionWS = positionWS;
    geoData.vertexNormal = normalize(normalWS);

    geoData.tangents[0] = tangentWS;
    geoData.tangents[1] = tangentWS;

    geoData.bitangents[0] = bitangentWS;
    geoData.bitangents[1] = bitangentWS;

    geoData.uvs = uvs;
    geoData.isFrontFace = isFrontFace;

    if (evaluateTangentFrame)
    {
        for (int i = 0; i != UvSetCount; ++i)
        {
            EvaluateTangentFrame(geoData.vertexNormal, positionWS, isFrontFace, uvs[i], i,
                tangentWS, bitangentWS, geoData.tangents[i], geoData.bitangents[i]);
        }
    }

    return geoData;
}

PixelGeometryData EvaluatePixelGeometry_Eye(
    float3 positionWS,
    real3 normalWS,
    real3 tangentWS,
    real3 bitangentWS,
    float2 uv[UvSetCount],
    float3 localPosition,
    bool isFrontFace)
{
    // Base PBR Geo Data
    bool evaluateTangentFrame = o_iris_o_normal_useTexture || o_sclera_o_normal_useTexture;
    PixelGeometryData geoData = EvaluatePixelGeometry_Eye(positionWS, normalWS, tangentWS, bitangentWS, uv, isFrontFace, evaluateTangentFrame);

    // Skin specifics
    geoData.localPosition = localPosition;

    return geoData;
}

PixelGeometryData EvaluatePixelGeometry_Eye(VsOutput IN, bool isFrontFace)
{
    real4x4 objectToWorld = real4x4(GetObjectToWorldMatrix(IN.m_instanceId));
    real3x3 objectToWorldIT = real3x3(GetObjectToWorldMatrixInverseTranspose(IN.m_instanceId));

    real3 normalWS, tangentWS, bitangentWS;
    ConstructTBN(real3(IN.normal), real4(IN.tangent), objectToWorld, objectToWorldIT, normalWS, tangentWS, bitangentWS);

    // Bitangent is temporarily added back to fix the eye material screenshot test.
    bitangentWS = real3(normalize(mul(objectToWorld, float4(IN.bitangent, 0.0)).xyz));

    PixelGeometryData geoData = EvaluatePixelGeometry_Eye(
        IN.worldPosition,
        normalWS,
        tangentWS,
        bitangentWS,
        IN.uvs,
        IN.localPosition,
        isFrontFace);

    geoData.objectToWorld = objectToWorld;
    geoData.objectToWorldIT = objectToWorldIT;
    return geoData;
}
