/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#ifndef MULTILAYER
#define MULTILAYER 0
#endif

#ifndef ENABLE_ALPHA_CLIP
#define ENABLE_ALPHA_CLIP 0
#endif

struct VSInput
{
    float3 m_position : POSITION;
    float2 m_uv0 : UV0;
    float2 m_uv1 : UV1;

    // only used for parallax depth calculation
    float3 m_normal : NORMAL;
    float4 m_tangent : TANGENT;

#if MULTILAYER
    // This gets set automatically by the system at runtime only if it's available.
    // There is a soft naming convention that associates this with o_blendMask_isBound, which will be set to true whenever m_optional_blendMask is available.
    // (search "m_optional_" in ShaderVariantAssetBuilder for details on the naming convention).
    // [GFX TODO][ATOM-14475]: Come up with a more elegant way to associate the isBound flag with the input stream.
    float4 m_optional_blendMask : COLOR0;
#endif
};
 
struct VSDepthOutput
{
    // "centroid" is needed for SV_Depth to compile
    precise linear centroid float4 m_position : SV_Position;
    float2 m_uv[UvSetCount] : UV1;

    // only used for parallax depth calculation
    float3 m_normal : NORMAL;
    float4 m_tangent : TANGENT;
    float3 m_worldPosition : UV0;

#if MULTILAYER
    float3 m_blendMask : UV3;
#endif
};

VSDepthOutput MainVS(VSInput IN)
{
    VSDepthOutput OUT;
 
    float4x4 objectToWorld = ObjectSrg::GetWorldMatrix();
    float4 worldPosition = mul(objectToWorld, float4(IN.m_position, 1.0));

    OUT.m_position = mul(ViewSrg::m_viewProjectionMatrix, worldPosition);

    OUT.m_uv[0] = mul(MaterialSrg::m_uvMatrix, float3(IN.m_uv0, 1.0)).xy;
    OUT.m_uv[1] = IN.m_uv1;

    OUT.m_worldPosition = worldPosition.xyz;
    OUT.m_normal = IN.m_normal;
    OUT.m_tangent = IN.m_tangent;

#if MULTILAYER
    if(o_blendMask_isBound)
    {
        OUT.m_blendMask = IN.m_optional_blendMask.rgb;
    }
    else
    {
        OUT.m_blendMask = float3(0,0,0);
    }
#endif

    return OUT;
}

struct PSDepthOutput
{
    precise float m_depth : SV_Depth;
};

PSDepthOutput MainPS(VSDepthOutput IN, bool isFrontFace : SV_IsFrontFace)
{
    PSDepthOutput OUT;

    OUT.m_depth = IN.m_position.z;

    if(ShouldHandleParallaxInDepthShaders())
    {
        float4x4 objectToWorld = ObjectSrg::GetWorldMatrix();
        float3x3 objectToWorldIT = ObjectSrg::GetWorldMatrixInverseTranspose();

        float3 vertexNormal, vertexTangent, vertexBitangent;
        ConstructTBN(IN.m_normal, IN.m_tangent, objectToWorld, objectToWorldIT, vertexNormal, vertexTangent, vertexBitangent);

        float3 tangents[UvSetCount] = { vertexTangent, vertexTangent };
        float3 bitangents[UvSetCount] = { vertexBitangent, vertexBitangent };

        for (int i = 0; i != UvSetCount; ++i)
        {
            EvaluateTangentFrame(
                vertexNormal,
                IN.m_worldPosition, 
                isFrontFace,
                IN.m_uv[i],
                i,
                vertexTangent,
                vertexBitangent,
                tangents[i],
                bitangents[i]);
        }

#if MULTILAYER
        MultilayerSetPixelDepth(IN.m_blendMask, IN.m_worldPosition, vertexNormal, tangents, bitangents, IN.m_uv, isFrontFace, OUT.m_depth);
#else
        SetPixelDepth(IN.m_worldPosition, vertexNormal, tangents, bitangents, IN.m_uv, isFrontFace, OUT.m_depth);
#endif
    }
    
#if ENABLE_ALPHA_CLIP
    GetAlphaAndClip(IN.m_uv);
#endif

    return OUT;
}
