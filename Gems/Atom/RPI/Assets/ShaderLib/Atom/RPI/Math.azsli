/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

#define PI      3.14159265
#define TWO_PI  6.28318530
#define INV_PI  0.31830988
#define EPSILON 10e-5

// Max values (largest positive values)

static const float FLOAT_32_MAX = 3.402823466e+38f;
static const real FLOAT_16_MAX = 65504.0;


// Min values (smallest positive values)

static const float FLOAT_32_MIN = 1.175494351e-38f;
static const real FLOAT_16_MIN = 0.000000059605f;



void swap(inout float a, inout float b)
{
    float c = a;
    a = b;
    b = c;
}

// ---------- Power -----------

float Pow2(float x)         // 1 multiplication
{
    return x * x;
}
float Pow3(float x)         // 2 multiplications
{
    return x * x * x;
}
float Pow4(float x)         // 2 multiplications
{
    x *= x;
    return x * x;
}
float Pow5(float x)         // 3 multiplications
{
    return x * Pow4(x);
}
float Pow6(float x)         // 3 multiplications
{
    x *= x;
    return x * x * x;
}
float Pow7(float x)         // 4 multiplications
{
    return x * Pow6(x);
}
float Pow8(float x)         // 3 multiplications
{
    x *= x;
    x *= x;
    return x * x;
}
float Pow9(float x)         // 4 multiplications
{
    x = x * x * x;
    return x * x * x;
}
float Pow10(float x)        // 4 multiplications
{
    x = Pow5(x);
    return x * x;
}

// ---------- Min & Max -----------

real min3(real a, real b, real c)
{
    return min(min(a, b), c);
}
real min4(real a, real b, real c, real d)
{
    return min(min3(a, b, c), d);
}
real max3(real a, real b, real c)
{
    return max(max(a, b), c);
}
real max4(real a, real b, real c, real d)
{
    return max(max3(a, b, c), d);
}

real min3(real3 abc)
{
    return min3(abc.x, abc.y, abc.z);
}
real min4(real4 abcd)
{
    return min4(abcd.x, abcd.y, abcd.z, abcd.w);
}
real max3(real3 abc)
{
    return max3(abc.x, abc.y, abc.z);
}
real max4(real4 abcd)
{
    return max4(abcd.x, abcd.y, abcd.z, abcd.w);
}

// ---------- Matrix ----------

real3x3 ExtractFloat3x3(real3x4 input)
{
    return real3x3(input[0].xyz, input[1].xyz, input[2].xyz);
}

real3x3 ExtractFloat3x3(float4x4 input)
{
    return real3x3(input[0].xyz, input[1].xyz, input[2].xyz);
}

// ---------- Intersection -----------

// a simple ray sphere intersection function, didn't take limited precision
// mentioned in Ray Tracing Gems : Chapter7 into consider for simplicity
// input vectors and calculation are in world space
// return length t to the closest hit point if hit, otherwise return -1
float RaySphereClosestHitWS(in float3 sphereOrigin, in float sphereRadius,
                          in float3 rayOrigin, in float3 rayDir)
{
    // vector point from sphere to ray origin
    float3 F = rayOrigin - sphereOrigin;
    float FdotF = dot(F, F);
    float FdotD = dot(F, rayDir);
    float DdotD = dot(rayDir, rayDir);

    float det = FdotD * FdotD - (DdotD * FdotF - (DdotD * sphereRadius * sphereRadius));

    if(det < 0.0f)
    {
        return -1;
    }

    float sqrtDet = sqrt(det);
    if(sqrtDet > -FdotD)
    {
        // return further hit, because closer hit is negative, i.e. behind ray origin
        return (-FdotD + sqrtDet) / DdotD;
    }
    else
    {
        return (-FdotD - sqrtDet) / DdotD;
    }
}

// returns true if the Aabb contains the specified point
bool AabbContainsPoint(real3 aabbMin, real3 aabbMax, real3 testPoint)
{
    real3 d = step(aabbMin, testPoint) - step(aabbMax, testPoint);
    return (d.x * d.y * d.z) > 0.0;
}

// returns the closest point on the Aabb to the specified point
real3 AabbClosestPoint(real3 aabbMin, real3 aabbMax, real3 testPoint)
{
    real3 closestPos = real3(0.0, 0.0, 0.0);
    for (uint i = 0; i < 3; ++i)
    {
        closestPos[i] = max(testPoint[i], aabbMin[i]);
        closestPos[i] = min(closestPos[i], aabbMax[i]);
    }
    return closestPos;
}

// computes [0..1] percentage of a point that's in between the inner and outer AABBs
real ComputeLerpBetweenInnerOuterAABBs(real3 innerAabbMin, real3 innerAabbMax, real3 outerAabbMax, real3 aabbPos, real3 position)
{
    real3 closestPointOnInnerAabb = AabbClosestPoint(innerAabbMin, innerAabbMax, position);

    // find the distance to the outer AABB by intersecting the ray from the closestPointOnInnerAabb through the position
    // to each plane of the AABB, and take the shortest hit - this is the closest AABB plane to our point

    // translate the points and outer AABB to the origin
    // take the abs() of the points to move them to the max quadrant of the outer AABB so everything is positive
    outerAabbMax -= aabbPos;
    position = abs(position - aabbPos);
    closestPointOnInnerAabb = abs(closestPointOnInnerAabb - aabbPos);
    real3 dirToPoint = normalize(position - closestPointOnInnerAabb);

    // intersect the ray from the closest point through the position with all three planes of the AABB
    // using outerAabbMax as the plane point and the plane normal defined in the array above (n.p = d)
    // Note: this is an optimized form of the following equation, performed simultaneously in all three planes:
    //       abs(dot(planeNormal, (outerAabbMax - position)) / dot(planeNormal, dirToPoint));
    real3 distance = abs(outerAabbMax - position) / dirToPoint;
    real shortestDistance = min(min(distance.x, distance.y), distance.z);

    // divide the distance by the total distance between the two intersect points
    // if (shortest distance == total distance) the blend is 1 since it's on the inner AABB
    // if (shortest distance == 0) the blend is 0 since it's on the outer AABB
    // if in between then it's the [0..1] percentage of the distance between inner and outer
    // if the inner and outer AABBs are identical then it's on both and the blend is 1
    real3 outerAabbIntersect = position + (dirToPoint * shortestDistance);
    real totalDistance = length(outerAabbIntersect - closestPointOnInnerAabb);
    return totalDistance > 0.0 ? saturate(shortestDistance / totalDistance) : 1.0;
}

// returns true if the Obb contains the specified point
bool ObbContainsPoint(real4x4 obbTransformInverse, real3 obbHalfExtents, real3 testPoint)
{
    // get the position in Obb local space, force to positive quadrant with abs()
    real3 p = abs(mul(obbTransformInverse, real4(testPoint, 1.0)).xyz);
    return AabbContainsPoint(-obbHalfExtents, obbHalfExtents, p);
}

// computes [0..1] percentage of a point that's in between the inner and outer OBBs
real ComputeLerpBetweenInnerOuterOBBs(real3x4 obbTransformInverse, real3 innerObbHalfExtents, real3 outerObbHalfExtents, float3 position)
{
    // get the position in Obb local space, force to positive quadrant with abs()
    real3 p = abs(mul(obbTransformInverse, real4(position, 1.0)));
    return ComputeLerpBetweenInnerOuterAABBs(-innerObbHalfExtents, innerObbHalfExtents, outerObbHalfExtents, real3(0.0, 0.0, 0.0), p);
}

// ---------- Normal Encoding -----------

// Encode/Decode functions for Signed Octahedron normals
// Store in a 10/10/10/2 texture for best results
// http://johnwhite3d.blogspot.com/2017/10/signed-octahedron-normal-encoding.html
float3 EncodeNormalSignedOctahedron(float3 normal)
{
    normal /= (abs(normal.x) + abs(normal.y) + abs(normal.z));

    float3 encodedNormal;
    encodedNormal.y = normal.y * 0.5f + 0.5f;
    encodedNormal.x = normal.x * 0.5f + encodedNormal.y;
    encodedNormal.y = normal.x * -0.5f + encodedNormal.y;
    encodedNormal.z = saturate(normal.z * FLOAT_32_MAX);
    return encodedNormal;
}

float3 DecodeNormalSignedOctahedron(float3 encodedNormal)
{
    float3 decodedNormal;
    decodedNormal.x = (encodedNormal.x - encodedNormal.y);
    decodedNormal.y = (encodedNormal.x + encodedNormal.y) - 1.0f;
    decodedNormal.z = encodedNormal.z * 2.0f - 1.0f;
    decodedNormal.z = decodedNormal.z * (1.0f - abs(decodedNormal.x) - abs(decodedNormal.y));
    decodedNormal = normalize(decodedNormal);
    return decodedNormal;
}

// Encode normal (assume normalized) to a texture coordinate within [0, 1] to save a channel
// not lossless, but error is negligible. This method is based on Lambert azimuthal projection which usually be used to project world map:
// https://en.wikipedia.org/wiki/Lambert_azimuthal_equal-area_projection
float2 EncodeNormalSphereMap(float3 normal)
{
    return normal.z <= (-1.0 + EPSILON) ? float2(0.0, 0.0) : float2(normal.xy / sqrt(normal.z * 8.0 + 8.0) + 0.5);
}

float3 DecodedNormalSphereMap(float2 encodedNormal)
{
    float2 fenc = encodedNormal * 4.0 - 2.0;
    float f = dot(fenc, fenc);
    float g = sqrt(1.0 - 0.25 * f);
    float3 normal;
    normal.xy = fenc * g;
    normal.z = 1.0 - 0.5 * f;
    // directly give out normal for invalid point at south pole, where the normal straightly points downward
    return (encodedNormal.x + encodedNormal.y) > EPSILON ? normal : float3(0.0, 0.0, -1.0);
}

// ---------- Unorm 2-bit Encoding -----------

// The four possible values in a UNORM 2-bit channel are encoded as: 0/3, 1/3, 2/3, 3/3
static const float UNORM_2BIT_FLAG1ONLY = 1.0f / 3.0f;
static const float UNORM_2BIT_FLAG2ONLY = 2.0f / 3.0f;
static const float UNORM_2BIT_BOTHFLAGS = 3.0f / 3.0f;
static const float UNORM_2BIT_EPSILON = 10e-3f;

// Encode two flags into a UNORM 2-bit channel
float EncodeUnorm2BitFlags(bool flag1, bool flag2)
{
    return (flag1 + (flag2 * 2)) / 3.0f;
}

// Decode flag1 from a UNORM 2-bit channel
bool DecodeUnorm2BitFlag1(float value)
{
    return (abs(value - UNORM_2BIT_FLAG1ONLY) < UNORM_2BIT_EPSILON) || (value == UNORM_2BIT_BOTHFLAGS);
}

// Decode flag2 from a UNORM 2-bit channel
bool DecodeUnorm2BitFlag2(float value)
{
    return (abs(value - UNORM_2BIT_FLAG2ONLY) < UNORM_2BIT_EPSILON) || (value == UNORM_2BIT_BOTHFLAGS);
}

// ---------- Quaternion -----------

float3 MultiplyVectorQuaternion(float3 v, float4 q)
{
    return v + 2.0 * cross(cross(v, q.xyz) + q.w * v, q.xyz);
}

half3 MultiplyVectorQuaternion(half3 v, half4 q)
{
    return v + 2.0 * cross(cross(v, q.xyz) + q.w * v, q.xyz);
}

// Rotates a vector using a rotation from a quaternion
// Assumes a right-handed coordinate system
float3 RotateVectorByQuaternion(float3 v, float4 q)
{
    return v + 2.0f * cross(q.xyz, cross(q.xyz, v) + q.w * v);
}

// ---------- Projection -----------

// This converts depth in clip space of perspective projection to linear depth in [0, 1].
// coefficients:
//   x : n * f
//   y : f - n
//   z : f
//   where n : nearDepth and f : farDepth.
// By the definition of the perspective projection,
//   (z_, w_)^T = P (z, w)^T
// where P = /  f/(n-f)    nf/(n-f)   \
//           \    -1          0       /.
// Then the inverse: z = n*f / ((f-n) * z_ - f). See DepthToLinearDepth.azsl. abs() here to keep positive value.
// Here is a link showing the relationship between clip depth and linear depth: https://www.desmos.com/calculator/tx9mz0qwzb
float PerspectiveDepthToLinear(float clipDepth, float3 coefficients)
{
    return abs(coefficients.x / (coefficients.y * clipDepth - coefficients.z));
}
half PerspectiveDepthToLinear(half clipDepth, half3 coefficients)
{
    return abs(coefficients.x / (coefficients.y * clipDepth - coefficients.z));
}

// ---------- Random number generators -----------

// George Marsaglia's Xorshift RNGs
// https://www.jstatsoft.org/article/view/v008i14/xorshift.pdf
uint Xorshift(in uint value)
{
    value ^= value << 13;
    value ^= value >> 17;
    value ^= value << 5;
    return value;
}

uint NextRandomUint(inout uint seed)
{
    seed = Xorshift(seed);
    return seed;
}

// Returns a random float with range [0, 1)
float NextRandomFloatUniform(inout uint seed)
{
    seed = Xorshift(seed);
    return (float)seed /  4294967295.0f;
}

//! Returns the inverse of lerp, 't', such that value = lerp(a, b, t), or returns 0 when a == b.
float LerpInverse(float a, float b, float value)
{
    if(abs(a - b) <= EPSILON)
    {
        return 0.0;
    }
    else
    {
        return (value - a) / (b - a);
    }
}

// -------- Gaussinan distribution functions ---------

// https://en.wikipedia.org/wiki/Gaussian_function
// Parameter:
// amplitude - the height of the curve's peak
// median - the position of the Mean / Median - the center of the "bell"
// standardDeviation - controls the width of the "bell"
float Gaussian(float x, float amplitude, float median, float standardDeviation)
{
    float exponent = -0.5 * Pow2((x - median) / standardDeviation);
    return amplitude * exp(exponent);
}
float GaussianNormalized(float x, float expectedValue, float standardDeviation)
{
    float normalizedAmplitude = 1.0f / (standardDeviation * sqrt(TWO_PI));
    return Gaussian(x, normalizedAmplitude, expectedValue, standardDeviation);
}

// ------- Useful functions -----------------

// Typical sigmoid function. Returns a value between 0.0 and 1.0.
real Sigmoid(real x)
{
    return 1.0 / (1.0 + exp(-x));
}

// This function returns a value between 0.0 and 1.0.
// This function is called "pseudo" sigmoid, because it is based
// on the sigmoid function, but has slope and inflection point
// control. Adding extra control parameters comes at the expense
// of asymmetry. To minimize the asymmetry we have the following
// restrictions (all parameters will be clamped accordingly):
// 0 < input
// 0.01 <=  inflection
// 0.0001 <= sigmoidWidth <= 0.1. If we allowed values greater than 0.1 the sigmoid
//                         starts to look asymmetrical around 'inflectionX'
// @param x The input variable. Expected to be larger than 0.0001.
// @param inflection The value in 'x' where the sigmoid reaches the inflection point and the output is 0.5.
// @param sigmoidWidth Width control of the sigmoid. The smaller this value is, the narrower (steeper slope) the sigmoid will be.
//                     For the minimum value of 0.0001 the sigmoid would look like a step function.
//                     Caveat: The larger the value of 'inflection' the wider the sigmoid.
real PseudoSigmoid(real x, real inflection, real sigmoidWidth)
{
    x = max(0.0001, x); // Avoids divide by zero
    inflection = max(0.01, inflection);

    // We limit the maximum value of sigmoidWidth to 0.1 because
    // the "pseudo" sigmoid becomes remarkably asymmetrical for values larger than 0.1.
    sigmoidWidth = clamp(sigmoidWidth, 0.0001, 0.1);
    return Sigmoid((x - inflection) / (x * sigmoidWidth));
}
