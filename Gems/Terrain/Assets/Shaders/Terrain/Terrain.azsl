/*
 * Copyright (c) Contributors to the Open 3D Engine Project. For complete copyright and license terms please see the LICENSE at the root of this distribution.
 * 
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#include <Atom/Features/SrgSemantics.azsli>
#include <viewsrg.srgi>


struct VertexInput
{
    float2 Position : POSITION;
    float2 UV : UV;
};

struct VertexOutput
{
    float4 Position : SV_Position;
    float3 Normal : NORMAL;
    float2 UV : UV;
};

ShaderResourceGroup ObjectSrg : SRG_PerObject
{
    Texture2D<float4> HeightmapImage;
    
    Sampler LinearSampler
    {
        MinFilter = Linear;
        MagFilter = Linear;
        MipFilter = Linear;
        AddressU = Clamp;
        AddressV = Clamp;
        AddressW = Clamp;
    };
    
    row_major float3x4 m_modelToWorld;
    float m_heightScale;
    float2 m_uvMin;
    float2 m_uvMax;
    float2 m_uvStep;
}

float4x4 GetObject_WorldMatrix()
{
    float4x4 modelToWorld = float4x4(
        float4(1, 0, 0, 0),
        float4(0, 1, 0, 0),
        float4(0, 0, 1, 0),
        float4(0, 0, 0, 1));

    modelToWorld[0] = ObjectSrg::m_modelToWorld[0];
    modelToWorld[1] = ObjectSrg::m_modelToWorld[1];
    modelToWorld[2] = ObjectSrg::m_modelToWorld[2];
    return modelToWorld;
}

float GetHeight(float2 origUv)
{
    float2 uv = clamp(origUv, 0.0f, 1.0f);
    return ObjectSrg::m_heightScale * (ObjectSrg::HeightmapImage.SampleLevel(ObjectSrg::LinearSampler, uv, 0).r - 0.5f);
}

VertexOutput MainVS(in VertexInput input)
{
    VertexOutput output;

    // Clamp the UVs *after* lerping to ensure that everything aligns properly right to the edge.
    // We use out-of-bounds UV values to denote vertices that need to be removed.
    float2 origUv = lerp(ObjectSrg::m_uvMin, ObjectSrg::m_uvMax, input.UV);
    float2 uv = clamp(origUv, 0.0f, 1.0f);

    // Loop up the height and calculate our final position.
    float height = GetHeight(uv);
    float3 worldPosition = mul(GetObject_WorldMatrix(), float4(input.Position, height, 1.0f)).xyz;
    output.Position = mul(ViewSrg::m_viewProjectionMatrix, float4(worldPosition, 1.0f));

    // Remove all vertices outside our bounds by turning them into NaN positions.
    output.Position = output.Position / ((origUv.x >= 0.0f && origUv.x < 1.0f && origUv.y >= 0.0f && origUv.y < 1.0f) ? 1.0f : 0.0f);

    // Calculate normal
    float2 gridSize = {1.0f, 1.0f};
    float up    = GetHeight(uv + ObjectSrg::m_uvStep * float2(-1.0f,  0.0f));
    float right = GetHeight(uv + ObjectSrg::m_uvStep * float2( 0.0f,  1.0f));
    float down  = GetHeight(uv + ObjectSrg::m_uvStep * float2( 1.0f,  0.0f));
    float left  = GetHeight(uv + ObjectSrg::m_uvStep * float2( 0.0f, -1.0f));

    float dydx = (right - left) * gridSize[0];
    float dydz = (down - up) * gridSize[1];
    
    output.Normal = normalize(float3(dydx, 2.0f, dydz));

    output.UV = uv;
    return output;
}

struct PixelOutput
{
     float4 m_color : SV_Target0;
};  
    
PixelOutput MainPS(in VertexOutput input)
{
    PixelOutput output;

   // Hard-coded fake light direction
    float3 lightDirection = normalize(float3(1.0, -1.0, 1.0));

    // Fake light intensity ranges from 1.0 for normals directly facing the light to zero for those
    // directly facing away.
    float lightDot = dot(normalize(input.Normal), lightDirection);
    float lightIntensity = lightDot * 0.5 + 0.5;

    // add a small amount of ambient and reduce direct light to keep in 0-1 range
    lightIntensity = saturate(0.1 + lightIntensity * 0.9);

    // The lightIntensity should not affect alpha so only apply it to rgb.
    //output.m_color.rgb = ((input.Normal + float3(1.0, 1.0, 1.0)) / 2.0);
    output.m_color.rgb = float3(1.0, 1.0, 1.0) * lightIntensity;
    output.m_color.a = 1.0f;

    return output;
}
