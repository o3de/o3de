/*
 * Copyright (c) Contributors to the Open 3D Engine Project. For complete copyright and license terms please see the LICENSE at the root of this distribution.
 * 
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#pragma once

#include <../Materials/Types/MaterialInputs/RoughnessInput.azsli>
#include <../Materials/Types/MaterialInputs/MetallicInput.azsli>
#include <../Materials/Types/MaterialInputs/SpecularInput.azsli>
#include <../Materials/Types/MaterialInputs/NormalInput.azsli>
#include <TerrainSrg.azsli>
#include <TerrainMaterialSrg.azsli>

ShaderResourceGroup ObjectSrg : SRG_PerObject
{
    struct PatchData
    {
        float2 m_xyTranslation;
        float m_xyScale;
        uint m_lodLevel;
    };

    PatchData m_patchData;

    // The below shouldn't be in this SRG but needs to be for now because the lighting functions depend on them.

    //! Reflection Probe (smallest probe volume that overlaps the object position)
    struct ReflectionProbeData
    {
        row_major float3x4 m_modelToWorld;
        row_major float3x4 m_modelToWorldInverse; // does not include extents
        float3 m_outerObbHalfLengths;
        float3 m_innerObbHalfLengths;
        float m_padding;
        bool m_useReflectionProbe;
        bool m_useParallaxCorrection;
        float m_exposure;
    };

    ReflectionProbeData m_reflectionProbeData;
    TextureCube m_reflectionProbeCubeMap;
    
    float4x4 GetReflectionProbeWorldMatrix()
    {
        float4x4 modelToWorld = float4x4(
            float4(1, 0, 0, 0),
            float4(0, 1, 0, 0),
            float4(0, 0, 1, 0),
            float4(0, 0, 0, 1));

        modelToWorld[0] = m_reflectionProbeData.m_modelToWorld[0];
        modelToWorld[1] = m_reflectionProbeData.m_modelToWorld[1];
        modelToWorld[2] = m_reflectionProbeData.m_modelToWorld[2];
        return modelToWorld;
    }

    float4x4 GetReflectionProbeWorldMatrixInverse()
    {
        float4x4 modelToWorldInverse = float4x4(
            float4(1, 0, 0, 0),
            float4(0, 1, 0, 0),
            float4(0, 0, 1, 0),
            float4(0, 0, 0, 1));

        modelToWorldInverse[0] = m_reflectionProbeData.m_modelToWorldInverse[0];
        modelToWorldInverse[1] = m_reflectionProbeData.m_modelToWorldInverse[1];
        modelToWorldInverse[2] = m_reflectionProbeData.m_modelToWorldInverse[2];
        return modelToWorldInverse;
    }
}

option bool o_useClipmap = false;

struct VertexInput
{
    float2 m_xyPosition : POSITION0;
    float1 m_zposition : POSITION1;
    float2 m_xyNormal : NORMAL;
};

float2 GetWorldXYPosition(in ObjectSrg::PatchData patchData, in float2 vertexPosition)
{
    return float2(patchData.m_xyTranslation + vertexPosition * patchData.m_xyScale);
}

bool IsVertexOutsideOfTerrainBounds(float2 position, float2 minBounds, float2 maxBounds)
{
    return (any(position < minBounds) || any(position > maxBounds));
}

void CalculateTerrainPosition(
    in SceneSrg::TerrainWorldData worldData, in ObjectSrg::PatchData patchData, in VertexInput input,
    out float3 worldPosition, out float4 position, out float2 cullDistance)
{
    float2 worldXyPosition = GetWorldXYPosition(ObjectSrg::m_patchData, input.m_xyPosition);
    if (IsVertexOutsideOfTerrainBounds(worldXyPosition, worldData.m_min.xy, worldData.m_max.xy))
    {
        // Output a NaN to remove this vertex.
        position = 1.0 / 0.0;
        return;
    }

    float heightScale = worldData.m_max.z - worldData.m_min.z;
    float height = worldData.m_min.z + input.m_zposition * heightScale;
    worldPosition = float3(GetWorldXYPosition(patchData, input.m_xyPosition), height);
    cullDistance = TerrainSrg::CalculateCullDistance(patchData.m_lodLevel, worldPosition);

    position = mul(ViewSrg::m_viewProjectionMatrix, float4(worldPosition, 1.0));
}
