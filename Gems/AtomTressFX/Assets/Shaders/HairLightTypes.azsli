/*
 * Modifications Copyright (c) Contributors to the Open 3D Engine Project. 
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 * 
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */
 
//------------------------------------------------------------------------------
//       Shader code related to lighting and shadowing for TressFX
//------------------------------------------------------------------------------

#pragma once

//==============================================================================
//                    Atom Lighting & Light Types
//==============================================================================
// Include options first
#include <Atom/Features/PBR/LightingOptions.azsli>
#include <HairSurface.azsli>
#include <HairUtilities.azsli>
#include <Atom/Features/PBR/Lighting/LightingData.azsli>

#include <Atom/Features/PBR/LightingUtils.azsli>
#include <Atom/Features/PBR/Microfacet/Brdf.azsli>
#include <Atom/Features/PBR/BackLighting.azsli>
//------------------------------------------------------------------------------
#include <Atom/Features/PBR/Decals.azsli>

#include <HairLightingEquations.azsli>
//===================== Hair Lighting Shader Options ===========================
enum class HairLightingModel {GGX, Marschner, Kajiya};
option HairLightingModel o_hairLightingModel = HairLightingModel::Marschner;

//==============================================================================
float3 GetSpecularLighting(Surface surface, LightingData lightingData, const real3 lightIntensity, const real3 dirToLight, uint viewIndex)
{
    float3 specular = float3(1, 0, 1);  // purple - error color
    if (o_hairLightingModel == HairLightingModel::GGX)
    {
        specular = SpecularGGX(lightingData.dirToCamera[viewIndex], dirToLight, surface.GetSpecularNormal(), surface.GetSpecularF0(), lightingData.GetSpecularNdotV(viewIndex), surface.roughnessA2, lightingData.multiScatterCompensation);
    }
    else if(o_hairLightingModel == HairLightingModel::Marschner)
    {
        specular = HairMarschnerBSDF(surface, lightingData, dirToLight, viewIndex);
    }
    // [To Do] - add the Kajiya-Kay lighting model option here in order to connect to Atom lighting loop

    return specular * lightIntensity;
}

float3 GetHairBackLighting(Surface surface, LightingData lightingData, float3 lightIntensity, float3 dirToLight, float shadowRatio)
{
    if (o_hairLightingModel == HairLightingModel::GGX)
    {
        float3 result = float3(0.0, 0.0, 0.0);
        float thickness = 0.0;
        float4 transmissionParams = surface.transmission.transmissionParams;

        // Thin object mode, using thin-film assumption proposed by Jimenez J. et al, 2010, "Real-Time Realistic Skin Translucency"
        // http://www.iryoku.com/translucency/downloads/Real-Time-Realistic-Skin-Translucency.pdf

        result = shadowRatio ?
            float3(0.0, 0.0, 0.0) :
            TransmissionKernel(surface.transmission.thickness * transmissionParams.w, rcp(transmissionParams.xyz)) *
            saturate(dot(-surface.GetDefaultNormal(), dirToLight)) * lightIntensity * shadowRatio;

        return result;
    }
    else // if ((o_hairLightingModel == HairLightingModel::Marschner) || (o_hairLightingModel == HairLightingModel::Kajiya))
    {
        return float3(0.0f, 0.0f, 0.0f);
    }

    return float3(1.0f, 0.0f, 0.0f);
}

//! Simple Lambertian BRDF
float3 HairDiffuseLambertian(float3 albedo, float3 normal, float3 dirToLight)
{
    float NdotL = saturate(dot(normal, dirToLight));
    return albedo * NdotL * INV_PI;
} 

// Replacing the generic Diffuse and Specular methods in StandardLighting.azsli 
// and removing the regular usage of clear coat second lobe energy distribution.
float3 GetDiffuseLighting(Surface surface, LightingData lightingData, float3 lightIntensity, float3 dirToLight)
{
    float3 diffuse = float3(0, 1, 0);   // Green - error color

    if (o_hairLightingModel == HairLightingModel::GGX)
    {
        // Notice that addition of the response (1-F) here
        diffuse = HairDiffuseLambertian(surface.albedo, surface.GetDiffuseNormal(), dirToLight) * lightingData.diffuseResponse;
    }
    else if (o_hairLightingModel == HairLightingModel::Marschner)
    {
        return float3(0.0f, 0.0f, 0.0f);
    }
    // [To Do] - add the Kajiya-Kay lighting model option here in order to connect to Atom lighting loop

    diffuse *= lightIntensity;   
    return diffuse;
}

void UpdateLightingParameters(
    inout LightingData lightingData, 
    float3 positionWS, float3 normal, float roughnessLinear, const float3 views[MAX_SHADING_VIEWS])
{

    [unroll]
    for(uint i = 0; i < GET_SHADING_VIEW_COUNT; ++i)
    {
        lightingData.dirToCamera[i] = normalize(views[i] - positionWS);

        // sample BRDF map (indexed by smoothness values rather than roughness)
        lightingData.NdotV[i] = saturate(dot(normal, lightingData.dirToCamera[i]));
        
        float2 brdfUV = float2(lightingData.GetSpecularNdotV(i), (1.0f - roughnessLinear));
        lightingData.brdf[i] = PassSrg::m_brdfMap.Sample(PassSrg::LinearSampler, brdfUV).rg;
    }
}

void SetNormalAndUpdateLightingParams(
    in float3 tangent, in float3 dirToLight, in const float3 views[MAX_SHADING_VIEWS],
    inout Surface surface, 
    inout LightingData lightingData)
{
    float3 biNormal;
    if (o_hairLightingModel == HairLightingModel::GGX)
    {   // Towards half vector but never cross fully (more weight to camera direction)
        float3 halfDir = normalize( dirToLight + 1.2 * lightingData.dirToCamera[0]);
        biNormal = normalize(cross(tangent, halfDir));
    }
    else
    {   // Face forward towards the camera
        biNormal = normalize(cross(tangent, lightingData.dirToCamera[0]));   
    }

    float3 projectedNormal = cross(biNormal, tangent);
    surface.normal = normalize(projectedNormal);    // the normalization might be redundunt 
    surface.vertexNormal = surface.normal; // [To Do] - support proper vertex normals in the hair shader.

    // Next is important in order to set NdotV and other PBR settings - needs to be set once per light
    UpdateLightingParameters(lightingData, surface.position, surface.GetSpecularNormal(), surface.roughnessLinear, views);

    // Diffuse and Specular response
    lightingData.specularResponse = FresnelSchlickWithRoughness(lightingData.GetSpecularNdotV(), surface.GetSpecularF0(), surface.roughnessLinear);
    lightingData.diffuseResponse = 1.0f - lightingData.specularResponse;
}

//==============================================================================
#include <Atom/Features/PBR/Lights/LightTypesCommon.azsli>
#include <Atom/Features/PBR/Lights/LightStructures.azsli>
//==============================================================================


//==============================================================================
//                         Simple Point Light 
//==============================================================================
void ApplySimplePointLight(SimplePointLight light, Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{  
    float3 posToLight = light.m_position - surface.position;
    float d2 = dot(posToLight, posToLight); // light distance squared
    float falloff = d2 * light.m_invAttenuationRadiusSquared;

    // Only calculate shading if light is in range
    if (falloff < 1.0f)
    {
        // Smoothly adjusts the light intensity so it reaches 0 at light.m_attenuationRadius distance
        float radiusAttenuation = 1.0 - (falloff * falloff);
        radiusAttenuation = radiusAttenuation * radiusAttenuation;

        // Standard quadratic falloff
        d2 = max(0.001 * 0.001, d2); // clamp the light to at least 1mm away to avoid extreme values.
        float3 lightIntensity = (light.m_rgbIntensityCandelas / d2) * radiusAttenuation;

        float3 dirToLight = normalize(posToLight);
        SetNormalAndUpdateLightingParams(surface.tangent, dirToLight, views, surface, lightingData);

        // Diffuse contribution
        lightingData.diffuseLighting += GetDiffuseLighting(surface, lightingData, lightIntensity, dirToLight);

        // Specular contribution
        [unroll]
        for (uint viewIndex = 0; viewIndex < GET_SHADING_VIEW_COUNT; ++viewIndex)
            lightingData.specularLighting[viewIndex] += GetSpecularLighting(surface, lightingData, lightIntensity, dirToLight, viewIndex);
    }
}

// The following function is currently not used and is written here for future testing.
// The culling scheme requires some more preperation and testing in HairLighting.azsli
// and once done, all light types can use the culling. 
// Not that the current culling scheme assumes light types lists order and is bogus if 
// some light types are not used or out of order (Atom's To Do list)
void ApplyCulledSimplePointLights(Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS], inout LightCullingTileIterator tileIterator)
{
    tileIterator.LoadAdvance();

    int lightCount = 0;
    while( !tileIterator.IsDone() && lightCount<1) 
    { 
        uint currLightIndex = tileIterator.GetValue(); 
        tileIterator.LoadAdvance();

        SimplePointLight light = ViewSrg::m_simplePointLights[currLightIndex];
        ApplySimplePointLight(light, surface, lightingData, views);
        ++lightCount;
    }
}

void ApplyAllSimplePointLights(Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{
    for (int l = 0; l < ViewSrg::m_simplePointLightCount; ++l)
    {
        SimplePointLight light = ViewSrg::m_simplePointLights[l];
        ApplySimplePointLight(light, surface, lightingData, views);
    }
}

//==============================================================================
//                           Simple Spot Light 
//==============================================================================
void ApplySimpleSpotLight(SimpleSpotLight light, Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{
    float3 posToLight = light.m_position - surface.position;
    float3 dirToLight = normalize(posToLight);
    float dotWithDirection = dot(dirToLight, -light.m_direction);

    // If outside the outer cone angle return.
    if (dotWithDirection < light.m_cosOuterConeAngle)
    {
        return;
    }

    float d2 = dot(posToLight, posToLight); // light distance squared
    float falloff = d2 * light.m_invAttenuationRadiusSquared;

    // Only calculate shading if light is in range
    if (falloff < 1.0f)
    {
        // Smoothly adjusts the light intensity so it reaches 0 at light.m_attenuationRadius distance
        float radiusAttenuation = 1.0 - (falloff * falloff);
        radiusAttenuation = radiusAttenuation * radiusAttenuation;

        // Standard quadratic falloff
        d2 = max(0.001 * 0.001, d2); // clamp the light to at least 1mm away to avoid extreme values.
        float3 lightIntensity = (light.m_rgbIntensityCandelas / d2) * radiusAttenuation;

        if (dotWithDirection < light.m_cosInnerConeAngle) // in penumbra
        {   
            // Normalize into 0.0 - 1.0 space.
            float penumbraMask = (dotWithDirection - light.m_cosOuterConeAngle) / (light.m_cosInnerConeAngle - light.m_cosOuterConeAngle);

            // Apply smoothstep
            penumbraMask = penumbraMask * penumbraMask * (3.0 - 2.0 * penumbraMask);

            lightIntensity *= penumbraMask;
        }

        SetNormalAndUpdateLightingParams(surface.tangent, dirToLight, views, surface, lightingData);

        // Tranmission contribution
        lightingData.translucentBackLighting += GetHairBackLighting(surface, lightingData, lightIntensity, dirToLight, 1.0);

        // Diffuse contribution
        lightingData.diffuseLighting += GetDiffuseLighting(surface, lightingData, lightIntensity, dirToLight);

        // Specular contribution
        [unroll]
        for (uint viewIndex = 0; viewIndex < GET_SHADING_VIEW_COUNT; ++viewIndex)
            lightingData.specularLighting[viewIndex] += GetSpecularLighting(surface, lightingData, lightIntensity, dirToLight, viewIndex);
    }
}

void ApplyAllSimpleSpotLights(Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{
    for (int l = 0; l < ViewSrg::m_simpleSpotLightCount; ++l)
    {
        SimpleSpotLight light = ViewSrg::m_simpleSpotLights[l];
        ApplySimpleSpotLight(light, surface, lightingData, views);
    }
}
//==============================================================================
//                              Point Light 
//==============================================================================
void ApplyPointLight(PointLight light, Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{
    float3 posToLight = light.m_position - surface.position;
    float d2 = dot(posToLight, posToLight); // light distance squared
    float falloff = d2 * light.m_invAttenuationRadiusSquared;

    // Only calculate shading if light is in range
    if (falloff < 1.0f)
    {
        // Smoothly adjusts the light intensity so it reaches 0 at light.m_attenuationRadius distance
        float radiusAttenuation = 1.0 - (falloff * falloff);
        radiusAttenuation = radiusAttenuation * radiusAttenuation;

        // Standard quadratic falloff
        d2 = max(0.001 * 0.001, d2); // clamp the light to at least 1mm away to avoid extreme values.
        float3 lightIntensity = (light.m_rgbIntensityCandelas / d2) * radiusAttenuation;

        float3 dirToLight = normalize(posToLight);
        SetNormalAndUpdateLightingParams(surface.tangent, dirToLight, views, surface, lightingData);

        // Diffuse contribution
        lightingData.diffuseLighting += GetDiffuseLighting(surface, lightingData, lightIntensity, dirToLight);

        // Tranmission contribution
        lightingData.translucentBackLighting += GetHairBackLighting(surface, lightingData, lightIntensity, dirToLight, 1.0);

        // Adjust the light direcion for specular based on bulb size

        // Calculate the reflection off the normal from the view direction
        float3 reflectionDir = reflect(-lightingData.dirToCamera[0], surface.GetSpecularNormal());

        // Calculate a vector from the reflection vector to the light
        float3 reflectionPosToLight = posToLight - dot(posToLight, reflectionDir) * reflectionDir;

        // Adjust the direction to light based on the bulb size
        posToLight -= reflectionPosToLight * saturate(light.m_bulbRadius / length(reflectionPosToLight));

        // Adjust the intensity of the light based on the bulb size to conserve energy
        float sphereIntensityNormalization = GetIntensityAdjustedByRadiusAndRoughness(surface.roughnessA, light.m_bulbRadius, d2);

        // Specular contribution
        [unroll]
        for (uint viewIndex = 0; viewIndex < GET_SHADING_VIEW_COUNT; ++viewIndex)
            lightingData.specularLighting[viewIndex] += sphereIntensityNormalization * GetSpecularLighting(surface, lightingData, lightIntensity, normalize(posToLight), viewIndex);
    }
}

void ApplyAllPointLights(Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{
    for (int l = 0; l < ViewSrg::m_pointLightCount; ++l)
    {
        PointLight light = ViewSrg::m_pointLights[l];
        ApplyPointLight(light, surface, lightingData, views);
    }
}

//==============================================================================
//                             Disk Lights
//==============================================================================
#include <Atom/Features/PBR/Lights/DiskLight.azsli>
#include <Atom/Features/Pipeline/Forward/ForwardPassDiskLights.azsli>

void ApplyAllDiskLights(Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{    
    SetNormalAndUpdateLightingParams(surface.tangent, lightingData.dirToCamera[0], views, surface, lightingData);
    for (int l = 0; l < ViewSrg::m_diskLightCount; ++l)
    {
        // Note: this doesn't use our GetHairBackLighting() - function
        ApplyDiskLight(l, surface, lightingData);
    }
}

//==============================================================================
//                            Directional Lights
//==============================================================================
void ApplyDirectionalLights(Surface surface, inout LightingData lightingData, float4 screenUv, const float3 views[MAX_SHADING_VIEWS])
{
    // Shadowed check
    const uint shadowIndex = ViewSrg::m_shadowIndexDirectionalLight;
    float litRatio = 1.0f;
    float backShadowRatio = 0.0f;

    SetNormalAndUpdateLightingParams(surface.tangent, -lightingData.dirToCamera[0], views, surface, lightingData);

    if (o_enableShadows && shadowIndex <  SceneSrg::m_directionalLightCount)
    {
        litRatio = DirectionalLightShadow::GetVisibility(
            shadowIndex,
            surface.position,
            surface.vertexNormal,
            screenUv);
    }

    // Add the lighting contribution for each directional light
    for (int index = 0; index < SceneSrg::m_directionalLightCount; index++)
    {
        DirectionalLight light = SceneSrg::m_directionalLights[index];
        float3 dirToLight = normalize(-light.m_direction);

        // Adjust the direction of the light based on its angular diameter.
        float3 reflectionDir = reflect(-lightingData.dirToCamera[0], surface.GetSpecularNormal());
        float3 lightDirToReflectionDir = reflectionDir - dirToLight;
        float lightDirToReflectionDirLen = length(lightDirToReflectionDir);
        lightDirToReflectionDir = lightDirToReflectionDir / lightDirToReflectionDirLen; // normalize the length
        lightDirToReflectionDirLen = min(light.m_angularRadius, lightDirToReflectionDirLen);
        dirToLight += lightDirToReflectionDir * lightDirToReflectionDirLen;
        dirToLight = normalize(dirToLight);

        float currentLitRatio = 1.0f;
        float currentBackShadowRatio = 1.0f;
        if (o_enableShadows)
        {
            currentLitRatio = (index == shadowIndex) ? litRatio : 1.0f;

            currentBackShadowRatio = 1.0 - currentLitRatio;
            if (o_transmission_mode == TransmissionMode::ThickObject)
            {
                currentBackShadowRatio = (index == shadowIndex) ?  backShadowRatio : 0.;
            }
        }

        lightingData.diffuseLighting += GetDiffuseLighting(surface, lightingData, light.m_rgbIntensityLux, dirToLight) * currentLitRatio;
        lightingData.translucentBackLighting += GetHairBackLighting(surface, lightingData, light.m_rgbIntensityLux, dirToLight, currentBackShadowRatio);

        [unroll]
        for (uint viewIndex = 0; viewIndex < GET_SHADING_VIEW_COUNT; ++viewIndex)
            lightingData.specularLighting[viewIndex] += GetSpecularLighting(surface, lightingData, light.m_rgbIntensityLux, dirToLight, viewIndex) * currentLitRatio;
    }
}

//==============================================================================
//                         IBL - GI and Reflections
//==============================================================================
// Adding diffuse contribution from IBL to the hair - this part still requires 
// multiple passes and improvements for both diffuse and specular IBL components.
// The immediate things to improve:
// - Virtual direction to 'light'
// - Absorption function based on hair accumulated thickness (back to front) and 
//      reverse thickness (front to back)
// - Diffuse contribution elements / scaling
float3 ApplyIblDiffuse(Surface surface, LightingData lightingData)
{
    float3 irradianceDir = MultiplyVectorQuaternion(surface.GetDiffuseNormal(), SceneSrg::m_iblOrientation);
    float3 diffuseSample = SceneSrg::m_diffuseEnvMap.Sample(SceneSrg::m_samplerEnv, GetCubemapCoords(irradianceDir)).rgb;

//    float3 diffuseLighting = HairDiffuseLambertian(surface.albedo, surface.GetDiffuseNormal(), lightingData.dirToCamera[0]) * lightingData.diffuseResponse * diffuseSample;
//    float3 diffuseLighting = GetDiffuseLighting(surface, lightingData, diffuseSample, surface.GetDiffuseNormal());

    // Notice the multiplication with inverse thickness used as a measure of occlusion
    return lightingData.diffuseResponse * surface.albedo * diffuseSample * (1.0f - surface.thickness);
}

float3 ApplyIblSpecular(Surface surface, LightingData lightingData, const uint viewIndex)
{
    float3 reflectDir = reflect(-lightingData.dirToCamera[0], surface.GetSpecularNormal());
    reflectDir = MultiplyVectorQuaternion(reflectDir, SceneSrg::m_iblOrientation);    

    // global
    float3 specularSample = SceneSrg::m_specularEnvMap.SampleLevel(
        SceneSrg::m_samplerEnv, GetCubemapCoords(reflectDir), 
        GetRoughnessMip(surface.roughnessLinear)).rgb;

    float3 specularLighting = GetSpecularLighting(surface, lightingData, specularSample, reflectDir, viewIndex);
    return specularLighting;
}

// Remark: IBL is still WIP and this part will change in the near future
void ApplyIBL(Surface surface, inout LightingData lightingData, const float3 views[MAX_SHADING_VIEWS])
{
//    float3 normal = normalize(float3(surface.tangent.z, -surface.tangent.x, surface.tangent.y));
//    SetNormalAndUpdateLightingParams(surface.tangent, normal, surface, lightingData);
    SetNormalAndUpdateLightingParams(surface.tangent, lightingData.dirToCamera[0], views, surface, lightingData);

    float3 iblDiffuse = ApplyIblDiffuse(surface, lightingData);

    // Adjust IBL lighting by exposure.
    float iblExposureFactor = pow(2.0, SceneSrg::m_iblExposure);
    lightingData.diffuseLighting += (iblDiffuse * iblExposureFactor * lightingData.diffuseAmbientOcclusion);

    [unroll]
    for (uint viewIndex = 0; viewIndex < GET_SHADING_VIEW_COUNT; ++viewIndex)
    {
        float3 iblSpecular = ApplyIblSpecular(surface, lightingData, viewIndex);
        lightingData.specularLighting[viewIndex] += (iblSpecular * iblExposureFactor);
    }
}


//==============================================================================
//                         
//                       Light Types Application
// 
//==============================================================================
void ApplyLighting(inout Surface surface, inout LightingData lightingData, float4 screenCoords, const float3 views[MAX_SHADING_VIEWS], inout LightCullingTileIterator tileIterator)
{
    // Shadow coordinates generation for the directional light
    const uint shadowIndex = ViewSrg::m_shadowIndexDirectionalLight;

    // Light loops application.
    // If culling is used, the order of the calls must match the light types list order 
//    ApplyDecals(tileIterator, surface);

    if (o_enableDirectionalLights)
    {
        ApplyDirectionalLights(surface, lightingData, screenCoords, views);
    }

    if (o_enablePunctualLights)
    {
        ApplyAllSimplePointLights(surface, lightingData, views);
        ApplyAllSimpleSpotLights(surface, lightingData, views);
    }

    if (o_enableAreaLights)
    {
        ApplyAllPointLights(surface, lightingData, views);
        ApplyAllDiskLights(surface, lightingData, views);
    }

    if (o_enableIBL)
    {
        ApplyIBL(surface, lightingData, views); 
    }
}
